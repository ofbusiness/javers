apply plugin: 'maven-publish'

dependencies {
    compile project(':javers-core')
    compile "org.polyjdbc:polyjdbc:$polyjdbcVersion"
    compile "com.google.guava:guava:$guavaVersion"

    testCompile project(path: ":javers-core", configuration: "testArtifacts")

    //required for @javax.persistence.Id ann in SnapshotEntity.class
    testCompile 'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final'

    testCompile 'commons-dbcp:commons-dbcp:1.4'
    testCompile 'com.h2database:h2:1.4.187'
    testCompile 'org.postgresql:postgresql:9.4-1201-jdbc41'
    testCompile 'org.mariadb.jdbc:mariadb-java-client:2.2.3'
    testCompile 'org.codehaus.gpars:gpars:1.2.1'
    testCompile "joda-time:joda-time:$jodaVersion"
    testCompile group: 'com.microsoft.sqlserver', name: 'mssql-jdbc', version: '6.1.0.jre8'

}

task integrationTest(type: Test) {
    filter {
        includeTestsMatching 'org.javers.repository.sql.integration.opendatabases.*'
    }
    testLogging {
        exceptionFormat = 'full'
        events 'failed', 'passed'
    }
}

task integrationTestMs(type: Test) {
    dependencies{
        testCompile files('../libs/sqljdbc4.jar') //sqljdbc4.jar should be manually copied to project /libs and .gitingored
    }
    filter {
        includeTestsMatching 'org.javers.repository.sql.integration.mssql.*'
    }
}

task integrationTestOracle(type: Test) {
    dependencies{
        testCompile files('../libs/ojdbc6.jar') //ojdbc6.jar should be manually copied to project /libs and .gitingored
        testCompile files('../libs/xdb6.jar')
    }
    filter {
        includeTestsMatching 'org.javers.repository.sql.integration.oracle.*'
    }
}

test {
    exclude 'org/javers/repository/sql/integration/**'
}

publishing {
    repositories {
        maven {
            credentials {
                username "$publisherUsername"
                password "$publisherPassword"
            }
            if (project.version.endsWith("-SNAPSHOT")) {
                url "$repositoryUrl/libs-snapshot"
            } else {
                url "$repositoryUrl/libs-release"
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}